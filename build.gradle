import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'io.freefair.lombok' version '8.11'
}

group = 'me.swiftens'
version = '1.3'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = "https://repo.jeff-media.com/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT")
    compileOnly('me.clip:placeholderapi:2.11.6')
    implementation("net.kyori:adventure-api:4.17.0")
    implementation("net.kyori:adventure-platform-bukkit:4.3.4")
    implementation("net.kyori:adventure-text-minimessage:4.17.0")
    implementation("com.jeff_media:SpigotUpdateChecker:3.0.3")
    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation("de.exlll:configlib-paper:4.5.0")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(ShadowJar).configureEach {

    archiveFileName.set("LoftyDailyRewards-${project.version}.jar")

    relocate("net.kyori", "me.swiftens.loftyDailyRewards.kyori")
    relocate("com.jeff_media", "me.swiftens.loftyDailyRewards.jeff_media")
    relocate("org.bstats", "me.swiftens.loftyDailyRewards.bstats")
    relocate("de.exlll", "me.swiftens.loftyDailyRewards.exlll")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
